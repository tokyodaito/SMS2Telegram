plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    // CI/local release signing:
    // - If SIGNING_* env vars (or Gradle properties) are provided, use them (stable key across builds).
    // - Otherwise, fall back to debug keystore so release APK is still installable (useful for GitHub releases),
    //   but signature may differ across environments.
    def signingStoreFile = System.getenv("SIGNING_STORE_FILE") ?: (project.findProperty("SIGNING_STORE_FILE") as String)
    def signingStorePassword = System.getenv("SIGNING_STORE_PASSWORD") ?: (project.findProperty("SIGNING_STORE_PASSWORD") as String)
    def signingKeyAlias = System.getenv("SIGNING_KEY_ALIAS") ?: (project.findProperty("SIGNING_KEY_ALIAS") as String)
    def signingKeyPassword = System.getenv("SIGNING_KEY_PASSWORD") ?: (project.findProperty("SIGNING_KEY_PASSWORD") as String)
    def hasReleaseSigning =
            signingStoreFile != null &&
            signingStorePassword != null &&
            signingKeyAlias != null &&
            signingKeyPassword != null

    signingConfigs {
        if (hasReleaseSigning) {
            release {
                storeFile file(signingStoreFile)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
                enableV1Signing true
                enableV2Signing true
            }
        }
    }

    defaultConfig {
        applicationId "life.hnj.sms2telegram"
        minSdk 30
        targetSdkVersion 34
        versionCode 1
        versionName "1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (hasReleaseSigning) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'life.hnj.sms2telegram'
    compileSdk 34
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1")
    implementation("androidx.work:work-runtime-ktx:2.9.0")
    implementation("com.android.volley:volley:1.2.1")
    implementation("io.coil-kt:coil:2.6.0")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
